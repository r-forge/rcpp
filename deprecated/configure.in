## -*- mode: autoconf++; autoconf-indentation: 4; -*-
##
## configure.in: R/C++ interface class library -- autoconf support
##
## Copyright (C) 2010  Dirk Eddelbuettel and Romain Francois
##
## This file is part of Rcpp.
##
## Rcpp is free software: you can redistribute it and/or modify it
## under the terms of the GNU General Public License as published by
## the Free Software Foundation, either version 2 of the License, or
## (at your option) any later version.
##
## Rcpp is distributed in the hope that it will be useful, but
## WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with Rcpp.  If not, see <http://www.gnu.org/licenses/>.

## This files owes a lot to other configure.in script cobbled together 
## over the years. Parts of the logic used here were contributed by
## Kurt Hornik for the RQuantLib package and its compiler and library 
## detection

## require at least autoconf 2.50
AC_PREREQ(2.50)

## Process this file with autoconf to produce a configure script.
AC_INIT(Rcpp, 0.7.2.3)

## Next segment once 'donated' by Kurt
: ${R_HOME=`R RHOME`}
if test -z "${R_HOME}"; then
   AC_MSG_ERROR([Could not determine R_HOME.])		
fi
## Make sure we use the CXX selected by R
CXX=`${R_HOME}/bin/R CMD config CXX`
CXXFLAGS=`"${R_HOME}/bin/R" CMD config CXXFLAGS`

## We are using C++
AC_LANG(C++)
AC_REQUIRE_CPP


## Basic check for R itself
AC_DEFUN(AC_PROG_R, [AC_CHECK_PROG(R,R,yes)])
AC_PROG_R

## look for Rscript, but use the one found via R_HOME to allow for multiple installations
AC_DEFUN(AC_PROG_RSCRIPT, [AC_CHECK_PROG(RSCRIPT,Rscript,yes)])
AC_PROG_RSCRIPT
if test x"${RSCRIPT}" == x"yes" ; then
     hasRscript=true
else
    echo "
  Your installation does not appear to have Rscript installed.

  Please make sure that you have a working and complete R installation.
"
    exit 1
fi

## The grep/sed command could probably be rewritten in Rscript as well
AC_PROG_CXX
if test "${GXX}" = yes; then
    gxx_version=`${CXX} -v 2>&1 | grep "^.*g.. version" | sed -e 's/^.*g.. version *//'`
    case ${gxx_version} in
        1.*|2.*)
	     AC_MSG_WARN([Only g++ version 3.0 or greater can be used with Rcpp.])
	     AC_MSG_ERROR([Please use a different compiler.])   
        ;;
        3.*|4.0.*|4.1.*)
	     AC_MSG_NOTICE([Only g++ version 4.2 or greater have been used for recent Rcpp development.])
	     AC_MSG_NOTICE([Please report any errors you may see to the maintainers.])   
	     has43=no
	     has44=no
        ;;
        4.2.*)
	     AC_MSG_NOTICE([With g++ version 4.2 core functionality should be stable -- but no C++0x features though].)
	     has43=no
	     has44=no
        ;;
        4.3.*)
	     AC_MSG_NOTICE([With g++ version 4.3 parts of the new Cxx0x standard are available.])
	     has43=yes
	     has44=no
        ;;
        4.*)
	     AC_MSG_NOTICE([With g++ version 4.4 or newer more parts of the new Cxx0x standard are available.])
	     has43=yes
	     has44=yes
        ;;
        *)
	     AC_MSG_NOTICE([Hm, not sure what g++ version you have, so using minimal features and no C++0x.])
	     has43=no
	     has44=no
        ;;
    esac
else
    AC_MSG_NOTICE([Hm, not sure what C++ compiler you have, so using minimal features and no C++0x.])
    has43=no
    has44=no
fi

# Offer a configure argument to enable support for the draft of the C++0x standard
AC_ARG_ENABLE([cxx0x], 
	      AC_HELP_STRING([--enable-cxx0x],[Enable support for draft of upcoming C++ standard. Can also be enabled by setting the 'RCPP_CXX0X' environment variable to 'yes' ]),
              [want_cxx0x=yes],
              [want_cxx0x=no])
## Mark 'RCPP_CXX0X' as a 'precious' variable
AC_ARG_VAR([RCPP_CXX0X],
           [Set to 'yes' to enable support for draft of upcoming C++ standard. Can also be enabled via the '--enable-cxx0x' option])

## Also try the RCPP_CXX0X environment variable
if test x"${want_cxx0x}" == x"no" ; then
   if test x"${RCPP_CXX0X}" == x"yes" ; then
   	want_cxx0x=yes
   fi
fi

## Let's default to not use the C++0x extentions
cxx0x_flags=""

## But if selected, then lets test to see if we can find a suitable compiler 
if test x"${want_cxx0x}" == x"yes" ; then
   AC_MSG_NOTICE([Selected Cxx0x support])
   ## Now use the 'has43' flag to add -std=c++0x to PKG_CXXFLAGS
   #if test x"${has43}" == x"yes" ; then
   #     cxx0x_flags="-std=c++0x"
   #fi

   AC_RUN_IFELSE([AC_LANG_PROGRAM([[
   #include <cstdio>
   #include <cstdlib>
   ]],[[
   int newEnough=1;		/* ie false */
   #ifdef __GNUC__
     #define GCC_VERSION (__GNUC__ * 10000 + __GNUC_MINOR__ * 100 + __GNUC_PATCHLEVEL__)
     #if GCC_VERSION >= 40300
       newEnough=0;             /* set to true */
     #endif
   #endif
   exit(newEnough);             /* return value */
   ]])],				 
   [gcc_version_ok=yes],
   [gcc_version_ok=no],
   [gcc_version_ok=yes])

   #AC_MSG_NOTICE(["Seeing ${gcc_version_ok}"])

   if test x"${gcc_version_ok}" == x"yes" ; then
       cxx0x_flags="-std=c++0x"
       AC_MSG_NOTICE([Setting Cxx0x support which is selected and available]) 
   else
       AC_MSG_NOTICE([Cxx0x support selected but not available]) 
   fi
	
else
   AC_MSG_NOTICE([No Cxx0x support selected])
fi

## now use cxx0x_flags
AC_SUBST([CXX0XFLAGS],["$cxx0x_flags"])
AC_OUTPUT(src/Makevars)
AC_MSG_NOTICE([Completed configuration and ready to build.])



